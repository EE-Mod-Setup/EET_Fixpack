BACKUP ~EET_Fix/backup~
AUTHOR ~Roxanne@baldursextendedworld.com~

VERSION ~v1~

BEGIN ~EET Fix~

//Black Pit creature fix
COPY ~EET_Fix/BPLLYRK.CRE~ ~override~
SAY NAME1 Llyrk
SAY NAME2 Llyrk

COMPILE ~EET_Fix/BPSKEL.BAF~

EXTEND_TOP
~LLyrk.BCS~ ~EET_Fix/LLyrk.baf~

//Set Chapter on ToB Transition safely
EXTEND_BOTTOM ~ar4000.bcs~ ~EET_Fix/ar4000.baf~

// Fix a couple of misaligned wall polygons in the FAI area (by Argent77)
OUTER_SET trans_y = 45 // vertical distance to move the wallpoly
COPY_EXISTING ~bg2300.WED~ ~override~
  READ_LONG 0x98 num_poly
  READ_LONG 0x9c ofs_poly
  READ_LONG 0xa0 ofs_vert

  // updating main poly
  DEFINE_ARRAY pt_y BEGIN 0 0 2899 2891 2887 2910 2944 0 0 0 0 END
  SET pofs = ofs_poly + 270 * 0x12
  READ_LONG pofs vidx
  READ_LONG (pofs + 4) vnum
  FOR (i = 0; i < vnum; ++i) BEGIN
    SET vofs = ofs_vert + (vidx + i) * 4
    PATCH_IF (VARIABLE_IS_SET $pt_y(~%i%~) AND $pt_y(~%i%~) != 0) BEGIN
      WRITE_SHORT (vofs + 2) (THIS + trans_y)
    END
  END

  // updating smaller polys of the portcullis
  DEFINE_ARRAY polys BEGIN 272 273 274 275 276 END
  PHP_EACH polys AS _ => pidx BEGIN
    SET pofs = ofs_poly + pidx * 0x12
    READ_LONG pofs vidx
    READ_LONG (pofs + 4) vnum
    FOR (i = 0; i < vnum; ++i) BEGIN
      SET vofs = ofs_vert + (vidx + i) * 4
      WRITE_SHORT (vofs + 2) (THIS + trans_y)
    END
    // bounding box
    WRITE_SHORT (pofs + 14) (THIS + trans_y)
    WRITE_SHORT (pofs + 16) (THIS + trans_y)
  END
BUT_ONLY IF_EXISTS

//Set Global on SoD Transition safely
EXTEND_TOP ~bd0120.bcs~ ~EET_Fix/bd0120cha.baf~

//Name Shadow Thief kidnapping scene
STRING_SET
239808 ~They all do, Mae'Var. For the moment.~

//Marek Potion
COPY ~EET_Fix/potn47.itm~ ~override~

//SoD Transition
COMPILE ~EET_Fix/BDSODTRN.BAF~

//Thalantyr skipped options repair
EXTEND_BOTTOM ~bg3202.bcs~ ~EET_Fix/Thalan.baf~

// WK Spirit Warrior
COPY ~EET_Fix/FSSPIR1.CRE~ ~override~

// DT level 2 spawn trap
COPY_EXISTING ~BG0512.are~ ~override~
WRITE_LONG 0x3bdc 10 
BUT_ONLY

// Drizzt Saga Compatibility
ACTION_IF FILE_EXISTS_IN_GAME ~F_Bel.cre~ THEN BEGIN
COPY_EXISTING ~F_Bel.cre~ ~override~
     SAY NAME1 Eltab
     SAY NAME2 Eltab
END

// Finch NPC Compatibility
ACTION_IF FILE_EXISTS_IN_GAME ~Sufinch4.cre~ THEN BEGIN
EXTEND_BOTTOM ~BD0103.bcs~ ~EET_Fix/BD0103.baf~
END

//Adoy in SoA Fix
EXTEND_TOP ~oh2010.bcs~ ~EET_Fix/OH2010.baf~

//Sahuagin rebel Fix
EXTEND_TOP ~ar2300.bcs~ ~EET_Fix/ar2300.baf~

ACTION_IF FILE_EXISTS ~SandrahNPC/cre/Sandrah.cre~ THEN BEGIN
ADD_KIT ~CVMystra~
~CVMystra               1           1           1           1           0           1           1           1~
~CVMystra 2 2 0 2 2 2 2 0 2 2 2 2 2 2 2 2 5 2 2 2 2 2 1 2 0 0 0 0 3 5 5 0 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2~
~CVMystra               12       0       0       8       10       10~
~CVMystra               0       0       0       0       0       0~
~CVMystra               0      0       0       0       0       0~
~CVMystra               15      0       0       0       0       0~
~CVMystra               0       0       1       1       0       0       0       0       0~
~CVMystra               0       0       0       0       0       0~
~SandrahNPC/Kit/CVMystra.2da~
~~
~0x00004000     3~
~FC0~
~* * * * * * * * * * * * * * * * * * * *~
SAY @967
SAY @765
SAY @766

INCLUDE ~SandrahNPC/Kit/fl#add_kit_ee.tpa~
LAF fl#add_kit_ee 
STR_VAR 
kit_name = CVMystra
END
END
